DEFINE_IMPORT(FileName=C:\PIXY_2.0\RECIPES\COMMONPOINTS.TXT)


DEFINE_VARIABLE(VariableName=ASP_UL, _
 VariableType=DOUBLE, _
 VariableValue=5)


DEFINE_VARIABLE(VariableName=PIEZO_AMP, _
 VariableType=INT, _
 VariableValue=50)


DEFINE_VARIABLE(VariableName=PIEZO_FREQ, _
 VariableType=INT, _
 VariableValue=13800)


DEFINE_VARIABLE(VariableName=PIEZO_DROPS, _
 VariableType=INT, _
 VariableValue=65)


DEFINE_VARIABLE(VariableName=PIEZO_DROPS_INS, _
 VariableType=INT, _
 VariableValue=100)


DEFINE_VARIABLE(VariableName=INS_OPTION, _
 VariableType=INT, _
 VariableValue=1)


' WORKHOLDER PITCH BETWEEN SPOTS AND NUMBER OF SPOTS


DEFINE_VARIABLE(VariableName=WORKHOLDER_COL_PITCH, _
 VariableType=DOUBLE, _
 VariableValue=1)


DEFINE_VARIABLE(VariableName=WORKHOLDER_NUM_COLS, _
 VariableType=INT, _
 VariableValue=4)


DEFINE_VARIABLE(VariableName=WORKHOLDER_ROW_PITCH, _
 VariableType=DOUBLE, _
 VariableValue=.8)


DEFINE_VARIABLE(VariableName=WORKHOLDER_NUM_ROWS, _
 VariableType=INT, _
 VariableValue=4)


' CENTER OF WORKHOLDER


DEFINE_VARIABLE(VariableName=WORKHOLDER_CX_T1, _
 VariableType=DOUBLE, _
 VariableValue=146)


DEFINE_VARIABLE(VariableName=WORKHOLDER_CY_T1, _
 VariableType=DOUBLE, _
 VariableValue=89.2)


' JUST TOUCHING WORKHOLDER SURFACE


DEFINE_VARIABLE(VariableName=WORKHOLDER_CZ_T1, _
 VariableType=DOUBLE, _
 VariableValue=-18.255)


' PITCH BETWEEN SPOTS,  X IS NEGATIVE,  Y IS POSITIVE


DEFINE_VARIABLE(VariableName=WORKHOLDER_PY, _
 VariableType=DOUBLE, _
 VariableValue=1.0)


' ORIGIN OF WORKHOLDER


DEFINE_VARIABLE(VariableName=WORKHOLDER_OX_T1, _
 VariableType=DOUBLE, _
 VariableValue=0.0)


DEFINE_VARIABLE(VariableName=WORKHOLDER_OY_T1, _
 VariableType=DOUBLE, _
 VariableValue=0.0)


DEFINE_VARIABLE(VariableName=WORKHOLDER_PX, _
 VariableType=DOUBLE, _
 VariableValue=0.0)


DEFINE_VARIABLE(VariableName=NEGATIVE_ONE, _
 VariableType=DOUBLE, _
 VariableValue=-1.0)


VARIABLE_MATH(WORKHOLDER_PX = WORKHOLDER_NUM_COLS - 1)


VARIABLE_MATH(WORKHOLDER_PX = WORKHOLDER_PX / 2)


VARIABLE_MATH(WORKHOLDER_PX = WORKHOLDER_COL_PITCH * WORKHOLDER_PX)


VARIABLE_MATH(WORKHOLDER_PX = WORKHOLDER_PX * NEGATIVE_ONE)


VARIABLE_MATH(WORKHOLDER_OX_T1 = WORKHOLDER_CX_T1 - WORKHOLDER_PX)


VARIABLE_MATH(WORKHOLDER_PY = WORKHOLDER_NUM_ROWS - 1)


VARIABLE_MATH(WORKHOLDER_PY = WORKHOLDER_PY / 2)


VARIABLE_MATH(WORKHOLDER_PY = WORKHOLDER_ROW_PITCH * WORKHOLDER_PY)


VARIABLE_MATH(WORKHOLDER_OX_T1 = WORKHOLDER_CX_T1 - WORKHOLDER_PX)


VARIABLE_MATH(WORKHOLDER_OY_T1 = WORKHOLDER_CY_T1 - WORKHOLDER_PY)


DEFINE_GRID(VariableName=WORKHOLDER_T1, _
 X=WORKHOLDER_OX_T1, _
 Y=WORKHOLDER_OY_T1, _
 Z=WORKHOLDER_CZ_T1, _
 XSteps=WORKHOLDER_NUM_COLS, _
 XIncrement=WORKHOLDER_PX, _
 YSteps=WORKHOLDER_NUM_ROWS, _
 YIncrement=WORKHOLDER_PY)


MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


DEFINE_VARIABLE(VariableName=WASH_CYCLES_B4_PICK, _
 VariableType=INT, _
 VariableValue=1)


DEFINE_VARIABLE(VariableName=WASH_CYCLES_FINAL, _
 VariableType=INT, _
 VariableValue=1)


DEFINE_VARIABLE(VariableName=I1, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=I1_MAX, _
 VariableType=INT, _
 VariableValue=0)


VARIABLE_MATH(I1_MAX = WORKHOLDER_NUM_COLS - 1)


DEFINE_VARIABLE(VariableName=I2, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=I2_MAX, _
 VariableType=INT, _
 VariableValue=0)


VARIABLE_MATH(I2_MAX = WORKHOLDER_NUM_ROWS - 1)


DEFINE_VARIABLE(VariableName=WASH_PROMPT, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=SAMPLE_ROW, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=SAMPLES_COL, _
 VariableType=INT, _
 VariableValue=0)


' WASH THE TIPS


FOR(I1 = 0 To I1_MAX Step 1)


	GET_INTEGER_FROM_USER(VariableName=SAMPLES_COL, _
	 Prompt=ENTER SAMPLE WELL COLUMN 1 TO 12 (CANCEL=END RECIPE)), _
	 MinValue=1, _
	 MaxValue=12, _
	 ValueIfUserCancels=0)


	IF(SAMPLES_COL = 0)


		MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=SAFE_PT, _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=TRUE)


		EXITSEQUENCE


	ENDIF


	VARIABLE_MATH(SAMPLES_COL = SAMPLES_COL - 1)


	GET_INTEGER_FROM_USER(VariableName=SAMPLE_ROW, _
	 Prompt=ENTER SAMPLE WELL ROW D=1  E=2  F=3  REPEAT SAMPLE=4 (CANCEL=END RECIPE), _
	 MinValue=1, _
	 MaxValue=4, _
	 ValueIfUserCancels=0)


	IF(SAMPLE_ROW = 0)


		MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=SAFE_PT, _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=TRUE)


		EXITSEQUENCE


	ENDIF


	IF(SAMPLE_ROW < 4)


		VARIABLE_MATH(SAMPLE_ROW = SAMPLE_ROW - 1)


		IF(WASH_CYCLES_B4_PICK > 0)


			GET_INTEGER_FROM_USER(VariableName=WASH_PROMPT, _
			 Prompt=ENTER 1=WASH (CANCEL=SKIP WASH), _
			 MinValue=1, _
			 MaxValue=1, _
			 ValueIfUserCancels=0)


			IF(WASH_PROMPT = 1)


				WASH_TIPS(SyringeMask=0, _
				 VolumePerStroke_uL=25, _
				 NumSyringeStrokes=WASH_CYCLES_B4_PICK, _
				 SyringeSpeedPull=13, _
				 SyringeSpeedPush=15, _
				 DelayAfter_ms=500, _
				 Tip1Point=WASH_PT_T1, _
				 MoveSpeedOut_pct=30)


				SYRINGE_SET_VALVE(SyringeMask=0, _
				 ValvePosition=1, _
				 DelayAfter_ms=100)


			ENDIF


		ENDIF


		' NOW ASPIRATE THE SAMPLE


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=PICKUP_SAMPLE_T1[SAMPLES_COL,SAMPLE_ROW], _
		 XOffset=0, _
		 YOffset=0, _
		 ZOffset=10, _
		 Speed_pct=50, _
		 MoveToSafeHeightFirst=TRUE)


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=PICKUP_SAMPLE_T1[SAMPLES_COL,SAMPLE_ROW], _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=FALSE)


		' ASPIRATE (FROM OUTPUT IS DEFAULT)


		ASPIRATE(SyringeMask=0, _
		 Volume_uL=ASP_UL, _
		 SyringeSpeed=18, _
		 ValveToBypass=TRUE, _
		 DelayAfter_ms=200)


		' PULL OUT SLOWLY


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=PICKUP_SAMPLE_T1[SAMPLES_COL,SAMPLE_ROW], _
		 XOffset=0, _
		 YOffset=0, _
		 ZOffset=10, _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=FALSE)


	ENDIF


	' MAKE SURE THE SYRINGE IS IN BYPASS


	SYRINGE_SET_VALVE(SyringeMask=0, _
	 ValvePosition=2, _
	 DelayAfter_ms=100)


	' INSPECT THE FIRING


	MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
	 Point=INS_PT_T1, _
	 XOffset=0, _
	 YOffset=0, _
	 ZOffset=0, _
	 Speed_pct=40, _
	 MoveToSafeHeightFirst=TRUE)


	VARIABLE_SET(VariableName=INS_OPTION, _
	 VariableValue=1)


	WHILE(INS_OPTION = 1)


		PIEZO_DISPENSE(Tip=1, _
		 PiezoAmplitude=PIEZO_AMP, _
		 PiezoFreq=PIEZO_FREQ, _
		 DropsPerBurst=PIEZO_DROPS_INS, _
		 NumBursts=1, _
		 FreqOfBursts=1)


		GET_INTEGER_FROM_USER(VariableName=INS_OPTION, _
		 Prompt=0=ABORT  1=FIRE PIEZO  2=PROCEED, _
		 MinValue=0, _
		 MaxValue=2, _
		 ValueIfUserCancels=1)


	LOOP


	' CANCEL WAS CHOSEN BY USER


	IF(INS_OPTION = 0)


		' SET AT OUTPUT, SYRINGE PUMP1


		SYRINGE_SET_VALVE(SyringeMask=0, _
		 ValvePosition=1, _
		 DelayAfter_ms=100)


		EXITSEQUENCE


	ENDIF


	' MOVE OVER THE WORKHOLDER


	MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
	 Point=WORKHOLDER_T1[I1,0], _
	 ZOffset=1, _
	 Speed_pct=20, _
	 MoveToSafeHeightFirst=TRUE)


	FOR(I2 = 0 To I2_MAX Step 1)


		' MOVE RIGHT ONTO TARGE


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=WORKHOLDER_T1[I1,I2], _
		 ZOffset=0, _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=FALSE)


		PIEZO_DISPENSE(Tip=1, _
		 PiezoAmplitude=PIEZO_AMP, _
		 PiezoFreq=PIEZO_FREQ, _
		 DropsPerBurst=PIEZO_DROPS, _
		 NumBursts=1, _
		 FreqOfBursts=1)


		PAUSE(UserMessage=WAIT, _
		 PauseTime_ms=600)


	NEXT


	MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


NEXT


MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=SAFE_PT, _
 Speed_pct=20, _
 MoveToSafeHeightFirst=TRUE)


PAUSE(UserMessage=REMOVE COVER SLIP  THEN HIT OK, _
 PauseTime_ms=0)


' FINAL WASH


IF(WASH_CYCLES_FINAL > 0)


	WASH_TIPS(SyringeMask=0, _
	 VolumePerStroke_uL=25, _
	 NumSyringeStrokes=WASH_CYCLES_FINAL, _
	 SyringeSpeedPull=13, _
	 SyringeSpeedPush=15, _
	 DelayAfter_ms=500, _
	 Tip1Point=WASH_PT_T1, _
	 MoveSpeedOut_pct=30)


	SYRINGE_SET_VALVE(SyringeMask=0, _
	 ValvePosition=1, _
	 DelayAfter_ms=100)


ENDIF


SYRINGE_SET_VALVE(SyringeMask=0, _
 ValvePosition=1, _
 DelayAfter_ms=100)


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=SAFE_PT, _
 Speed_pct=20, _
 MoveToSafeHeightFirst=TRUE)


