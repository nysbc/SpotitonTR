' WASH TIP 1 (PIEZO) WITH WATER


' PICKUP SAMPLE (SAMPLE NUMBER), TEST DISPENSE ON PAPER,


' DEPOSIT IN GRID ON MEMBRANE, SYRINGE TOUCHOFF METHOD


MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


DEFINE_POINT(VariableName=WASH_PT_T1, _
 X=5, _
 Y=0, _
 Z=-34)


DEFINE_POINT(VariableName=SAFE_PT, _
 X=5, _
 Y=0, _
 Z=0.0)


DEFINE_VARIABLE(VariableName=WASH_CYCLES, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=I1, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=I2, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=SAMPLE_NUMBER, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=ST_TESTDISPENSE_INDEX, _
 VariableType=INT, _
 VariableValue=0)


DEFINE_VARIABLE(VariableName=END_TESTDISPENSE_INDEX, _
 VariableType=INT, _
 VariableValue=2)


GET_INTEGER_FROM_USER(VariableName=SAMPLE_NUMBER, _
 Prompt=ENTER SAMPLE WELL NUMBER 0=A1  7=H1), _
 MINVALUE=0, _
 MaxValue=7, _
 ValueIfUserCancels=0)


' DEFINE AS 0 OR 1


DEFINE_VARIABLE(VariableName=TESTDISPENSE_XCOL, _
 VariableType=INT, _
 VariableValue=1)


GET_INTEGER_FROM_USER(VariableName=ST_TESTDISPENSE_INDEX, _
 Prompt=ENTER STARTING TEST DISPENSE INDEX, _
 MinValue=0, _
 MaxValue=19, _
 ValueIfUserCancels=0)


GET_INTEGER_FROM_USER(VariableName=END_TESTDISPENSE_INDEX, _
 Prompt=ENTER ENDING TEST DISPENSE INDEX, _
 MinValue=ST_TESTDISPENSE_INDEX, _
 MaxValue=20, _
 ValueIfUserCancels=ST_TESTDISPENSE_INDEX)


DEFINE_GRID(VariableName=PICKUP_SAMPLE, _
 X=136.0, _
 Y=124.0, _
 Z=-29, _
 XSteps=1, _
 XIncrement=9.0, _
 YSteps=8, _
 YIncrement=9.0)


DEFINE_GRID(VariableName=MEMBRANEGRID_T1, _
 X=86, _
 Y=89, _
 Z=-21.2, _
 XSteps=3, _
 XIncrement=2.0, _
 YSteps=3, _
 YIncrement=2.0)


DEFINE_GRID(VariableName=TESTDISPENS_T1, _
 X=88, _
 Y=12, _
 Z=-34.2, _
 XSteps=2, _
 XIncrement=2.0, _
 YSteps=20, _
 YIncrement=2.0)


' WASH THE TIPS (OPTIONALLY)


GET_INTEGER_FROM_USER(VariableName=WASH_CYCLES, _
 Prompt=ENTER NUMBER OF WASH CYCLES, _
 MinValue=0, _
 MaxValue=3, _
 ValueIfUserCancels=0)


IF(WASH_CYCLES > 0)


	WASH_TIPS(SyringeMask=0, _
	 VolumePerStroke_uL=50, _
	 NumSyringeStrokes=WASH_CYCLES, _
	 SyringeSpeedPull=15, _
	 SyringeSpeedPush=15, _
	 DelayAfter_ms=500, _
	 Tip1Point=WASH_PT_T1, _
	 MoveSpeedOut_pct=30)


ENDIF


' PICKUP SAMPLE


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=PICKUP_SAMPLE[0,SAMPLE_NUMBER], _
 Speed_pct=50, _
 MoveToSafeHeightFirst=TRUE)


' ASPIRATE (FROM OUTPUT IS DEFAULT)


ASPIRATE(SyringeMask=0, _
 Volume_uL=10, _
 SyringeSpeed=20, _
 ValveToBypass=FALSE, _
 DelayAfter_ms=500)


' PULL OUT SLOW


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=PICKUP_SAMPLE[0,SAMPLE_NUMBER], _
 XOffset=0, _
 YOffset=0, _
 ZOffset=8, _
 Speed_pct=20, _
 MoveToSafeHeightFirst=FALSE)


' DO TEST DISPENSING


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=TESTDISPENS_T1[TESTDISPENSE_XCOL,ST_TESTDISPENSE_INDEX], _
 XOffset=0, _
 YOffset=0, _
 ZOffset=2, _
 Speed_pct=30, _
 MoveToSafeHeightFirst=TRUE)


FOR(I1 = ST_TESTDISPENSE_INDEX To END_TESTDISPENSE_INDEX Step 1)


	MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
	 Point=TESTDISPENS_T1[TESTDISPENSE_XCOL,I1], _
	 ZOffset=2, _
	 Speed_pct=20, _
	 MoveToSafeHeightFirst=FALSE)


	DISPENSE(SyringeMask=0, _
	 Volume_uL=0.200, _
	 SyringeSpeed=15, _
	 ValveToBypass=FALSE, _
	 DelayAfter_ms=750)


	MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
	 Point=TESTDISPENS_T1[TESTDISPENSE_XCOL,I1], _
	 Speed_pct=20, _
	 MoveToSafeHeightFirst=FALSE)


	PAUSE(UserMessage=WAIT, _
	 PauseTime_ms=1000)


	MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
	 Point=TESTDISPENS_T1[TESTDISPENSE_XCOL,I1], _
	 ZOffset=2, _
	 Speed_pct=20, _
	 MoveToSafeHeightFirst=FALSE)


NEXT


PAUSE(UserMessage=CLICK OK TO CONTINUE, _
 PauseTime_ms=0)


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=MEMBRANEGRID_T1[0,0], _
 XOffset=0, _
 YOffset=0, _
 ZOffset=2, _
 Speed_pct=30, _
 MoveToSafeHeightFirst=TRUE)


FOR(I1 = 0 To 2 Step 1)


	FOR(I2 = 0 To 2 Step 1)


		' GO RIGHT ABOVE TARGET POINT


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=MEMBRANEGRID_T1[I1,I2], _
		 ZOffset=2, _
		 Speed_pct=20, _
		 MoveToSafeHeightFirst=FALSE)


		DISPENSE(SyringeMask=0, _
		 Volume_uL=0.200, _
		 SyringeSpeed=15, _
		 ValveToBypass=FALSE, _
		 DelayAfter_ms=200)


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=MEMBRANEGRID_T1[I1,I2], _
		 Speed_pct=30, _
		 MoveToSafeHeightFirst=FALSE)


		PAUSE(UserMessage=WAIT, _
		 PauseTime_ms=300)


		MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
		 Point=MEMBRANEGRID_T1[I1,I2], _
		 ZOffset=2, _
		 Speed_pct=30, _
		 MoveToSafeHeightFirst=FALSE)


	NEXT


NEXT


MOVE_TO_SAFE_HEIGHT(Speed_pct=30)


' WASH THE TIPS (OPTIONALLY)


GET_INTEGER_FROM_USER(VariableName=WASH_CYCLES, _
 Prompt=ENTER NUMBER OF WASH CYCLES, _
 MinValue=0, _
 MaxValue=3, _
 ValueIfUserCancels=0)


IF(WASH_CYCLES > 0)


	WASH_TIPS(SyringeMask=0, _
	 VolumePerStroke_uL=50, _
	 NumSyringeStrokes=WASH_CYCLES, _
	 SyringeSpeedPull=15, _
	 SyringeSpeedPush=15, _
	 DelayAfter_ms=500, _
	 Tip1Point=WASH_PT_T1, _
	 MoveSpeedOut_pct=30)


ENDIF


' SET AT OUTPUT, SYRINGE PUMP1


MOVE_TO_POINT(Tool=SYSTEM_TIP1, _
 Point=SAFE_PT, _
 Speed_pct=20, _
 MoveToSafeHeightFirst=TRUE)


SYRINGE_SET_VALVE(SyringeMask=0, _
 ValvePosition=1, _
 DelayAfter_ms=100)


